<?xml version="1.0" encoding="utf-8"?>
<s:Window xmlns:fx="http://ns.adobe.com/mxml/2009"
		  xmlns:s="library://ns.adobe.com/flex/spark"
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  xmlns:ns="yourdomain.modules.modularWindow_B.views.components.*"
		  width="200"
		  height="200"
		  title="MODULAR WINDOW B"
		  closing="window_closingHandler(event)"
		  close="window_closeHandler(event)"
		  preinitialize="preinitializeHandler(event)"
		  creationComplete="creationCompleteHandler(event)">
	<fx:Script>
		<![CDATA[
			import mx.events.FlexEvent;

			import yourdomain.modules.modularWindow_B.configs.ModularWindowContext_B;

			//============================================================================
			// create the Robotlegs Context
			//============================================================================
			private var _windowContext:ModularWindowContext_B;

			private function preinitializeHandler(event:FlexEvent):void
			{
				removeEventListener(FlexEvent.PREINITIALIZE, preinitializeHandler);

				_windowContext = new ModularWindowContext_B(this);
			}

			//============================================================================
			// add an event listener for the Spark Button
			//============================================================================
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				removeEventListener(FlexEvent.CREATION_COMPLETE, creationCompleteHandler);
				closeWindowButton.addEventListener(MouseEvent.CLICK, closeWindowButton_clickHandler);
			}

			//============================================================================
			// remove all event listeners and subcomponents
			//============================================================================
			public function cleanUp():void
			{
				closeWindowButton.removeEventListener(MouseEvent.CLICK, closeWindowButton_clickHandler);
				removeAllElements();
			}

			//============================================================================
			// close the window progranmatically
			//============================================================================
			protected function closeWindowButton_clickHandler(event:MouseEvent):void
			{
				close();
			}

			//============================================================================
			// handler for the Event.CLOSE dispatched after the window has been closed.
			// 

			// hack:
			// dispatch Event.REMOVED_FROM_STAGE to inform Robotlegs that it can remove the
			// Mediator for this View
			// 

			// because when TitleBar's closeButton is clicked the WindowedSystemManager
			// does NOT remove the window, thus no REMOVED_FROM_STAGE is dispatched
			//============================================================================
			protected function window_closeHandler(event:Event):void
			{
				if (stage)
				{
					dispatchEvent(new Event(Event.REMOVED_FROM_STAGE));
				}
				//cleanUp();
			}


			protected function window_closingHandler(event:Event):void
			{
				cleanUp();
			}
		]]>
	</fx:Script>
	<s:Button id="closeWindowButton"
			  x="10"
			  y="10"
			  label="close"/>
</s:Window>
